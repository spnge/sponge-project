/**  bake.bento
 *
 *   Project automation and dependency management in Bento
 *
 *
 **/

site bake [=

    /------------------------------------------/
    /-------------- version info  -------------/
    /------------------------------------------/

    public bake_version = "0.1"


    /------------------------------------------/
    /--------------   constants   -------------/
    /------------------------------------------/

    /** status values **/

    status [?]    
    status READY = "ready"
    status EXECUTING = "executing"
    status DONE = "done"
    status BLOCKED = "blocked"
    status SUCCEEDED = "succeeded"
    status FAILED = "failed" 


    /------------------------------------------/
    /-------------- session state -------------/
    /------------------------------------------/

    session_init [=
        left_panel_width(19);
        console_init();
        console_print("bake version " + bake_version);
    =]

    /** completion status of targets, can be READY, EXECUTING or DONE. Stored by 
     *  target name.
     **/
    status{} completion_status = {}
    
    /** set the completion status of a target **/
    dynamic set_completion_status(tname, status s) [=
        target_name = tname

        keep by target_name in completion_status:
        dynamic status save_status = s
        
        eval(save_status);
    =]
    
    /** set the completion status of all targets back to READY. **/
    dynamic clear_all_completion [=
        keep by tname in completion_status:
        status clear_completion = READY

        for tname in target_status.keys [=
            eval(clear_status);
        =]
    =]
    

    /** sussess status of targets, can be READY, EXECUTING, SUCCEEDED or 
     *  FAILED. Stored by target name.
     **/
    status{} success_status = {}
    
    /** set the success status of a target **/
    dynamic set_success_status(target_name, status s) [=
        keep by target_name in success_status:
        dynamic status save_status = s
        eval(save_status);
    =]  


    /------------------------------------------/
    /--------------      UI       -------------/
    /------------------------------------------/

    /---- site variables ----/

    float left_panel_width(float ems) = ems

    public color main_bgcolor = "#d5f1e5"

    dynamic class_for_status(status s) = "status_" + status
    
    /---- expanded/collapsed state of target group panels ----/
    
    boolean group_panel_expanded{} = {}
    
    dynamic set_group_panel_expanded(group_name, boolean expanded) [=
        grp_nm = group_name
        keep by grp_nm in group_panel_expanded: boolean set_expanded = expanded
        eval(set_expanded);
    =]

    /** base class of web page to display list of targets and instantiate selected targets. Bake
     *  projects should subclass this definition and add an implementation for top-level targets.
     **/

    public page bakepage [=
        boolean ajax_enabled = true
        title = sitename
        
        style [| 
            html, body { 
                width: 100%;
                height: 100%;
                margin: 0 0 0 0;
                background: [= main_bgcolor; =];
            }
            
            h2 {
                margin-left: 1em;
            }
            
            div {
                background: [= main_bgcolor; =];
            }
            
            #main_container {
                position: absolute;
                top: 0;
                left: 0;
                width: 100%;
                height: 100%;
                margin: 0;
                padding: 0;
            }

            .vertical_bar {
                height: 100%;
                width: 0.25em;
                background: #dddddd;  
                border-width: 0 0.125em 0 0.125em;
                border-style: outset;
                border-color: white;  
            }
            
            .expansion_widget {
                display: inline;
                height: 1em;
                width: 1em;
                padding: 0;
                margin: 0.125em 0.25em;
            }
            
            .expansion_widget button {
                background: [= main_bgcolor; =];
                padding: 0.125em;
                font-size: 0.675em;
                font-weight: bold;
                font-family: "Lucida Console", monospace;
                color: #111155;
            }
            
            .dependency_panel {
                margin-left: 2em;
                width: 12em;
            }
            
            .dependency_state {
                width: 1.25em;
            }

            .target_group_panel {
                border: 0.125em outset white;
            }

            .target_panel_container {
                border: 0.0675em outset white;
            }

            .target_panel {
                position: relative;
                width: 100%;
                margin: 0;
                padding:0.25em;
                border-top: 0.0675em outset red;
                border-bottom: 0.0675em outset red;
            }
            
            .target_state {
                position: absolute;
                left: 0;
                top: 0;
                width: 1.5em;
            }

            .target_selector {
                margin: 0 2em 0 2em;
            }
            
            .target_exec {
                position: absolute;
                right: 0;
                top: 0;
                width: 1.5em;
            }

            .status_box {
                height: 0.5em;
                width: 0.5em;
                min-height: 0.5em;
                min-width: 0.5em;
                border-width: 0.125em;
                border-style: inset;
                border-color: white;  
                margin: 0.5em 0.25em 0.5em 0.25em;
                padding: 0;
            }
            
            .status_ready {
                background: [= main_bgcolor; =];
            }
            
            .status_done {
                background: gray;
            }
            
            .status_blocked {
                background: red;
            }
            
            .status_executing {
                background: yellow;
            }
            
            .status_succeeded {
                background: lime;
            }
            
            .status_failed {
                background: red;
            }

            #left_panel {
                position: absolute;
                padding: 0;
                left: 0;
                top: 0;
                width: [= left_panel_width; =]em;
            }
            
            #size_bar {
                cursor: pointer;
                position: absolute;
                left: [= (left_panel_width + 0.5); =]em;
                top: 0;
                bottom: 0;
                z-index: 1001;
            }

            #right_panel {
                background: #dddddd;
                margin: 0;
                position: absolute;
                left: [= (left_panel_width + 1); =]em;
                top: 0;
                right: 0.5em;
                bottom: 0.5em;
            }
            
            #bake_console {
                width: 100%;
                height: 100%;
            }

            [= console_style; =]
        |]
        
        onload_script [=
            console_init_script("bake_console");
            size_bar_init_script;
        =]
            
        size_bar_init_script [|
            size_bar_init();            
        |] 
        
        expand_panel_script [|
            function expandPanel(expanderButton, group, expand) {
                var panelId = group + "_target_panel_container"
                var panel = document.getElementById(panelId);
                if (expand === "true") {
                    panel.style.display = "block";
                    expanderButton.innerHTML = "-";
                } else {
                    panel.style.display = "none";
                    expanderButton.innerHTML = "+";
                }
                var newExpand = (expand == "true" ? "false" : "true");
                expanderButton.onclick = function() {
                    expandPanel(expanderButton, group, newExpand);
                };
                var params = { "group": group, "expand": expand } 
                postToServer("inform", null, null, params);
            }

        |]
        
        target_param = "target"
        group_param = "group"
        expansion_param = "expand"
    
        /** top-level target groups (ones available to the user).  This must
         *  be overridden by subclasses.
         **/
        target_group[] all_target_groups = []
        
        /** target_group look up map; computed from all_target_groups array. **/
        target_group{} group_by_name = { for target_group tg in all_target_groups [= tg.group_name: tg =] }
        
        /** array of all targets; computed from all_target_groups. **/
        target[] all_targets = [ for target_group tg in all_target_groups [=
                                     for target t in tg.targets [=
                                         t,
                                         for target d in t.depends_on [=
                                             d
                                         =]
                                     =]
                                 =] ] 
    
        /** target look up map; computed from all_targets array. **/
        target{} target_by_name = { for target tgt in all_targets [= tgt.name: tgt =] }

        
        dynamic console(*) bake_console(request r) [=
            id = "bake_console"
            
            log("  - - ::> instantiating bake_console");
        =]

        component size_bar [=
            id = "size_bar"
            component_class = "vertical_bar"
        =]
        
        dynamic component status_box(status s) [=
            component_class = "status_box status_" + s
            sub;
        =]

        dynamic inform(params{}) [=
            boolean expand = (params{expansion_param} != "false")
            group_name = params{group_param}

            with (params{expansion_param}) [=
                log("expand " + group_name + ": " + expand);
                set_group_panel_expanded(group_name, expand);
            =]
        =]

        dynamic component expansion_widget(target_group_panel tgp, boolean expanded) [=
            component_class = "expansion_widget"
            button_value = expanded ? "-" : "+"
            button_name = "expand"

            [| <button type="button" name="[= button_name; =]"
                       onclick="expandPanel(this, '[= tgp.group_name; =]', '[= (!expanded); =]')">
            |]
            button_value;
            [| </button> |]
        =]


        dynamic component target_group_panel(target_group tg),(params{}) [=
            component_class = "target_group_panel"
            group_name [= with (tg) [=
                              tg.group_name;
                              log("group_name is tg.group_name: " + tg.group_name);
                          =] else [= 
                              params{group_param};
                              log("group_name is params{group_param}: " + params{group_param});
                          =]
                       =]
                          
            target_group tgt_grp = params ? group_by_name{group_name} : tg
            
            id = group_name + "_group_panel"
            target_panel_container_id = group_name + "_target_panel_container"
            
            dynamic boolean expanded = group_panel_expanded{group_name}
        
            
            [| <h3> |]
            expansion_widget(this, expanded);
            group_name;
            
            [| </h3><div class="target_panel_container" id=" |]
            target_panel_container_id;            

            [| " style="display: |]
            if (expanded) [| block |]
            else          [| none  |]
            [| "> |]

            for target t in tgt_grp.targets [=
                target_panel(t);
            =]
            [| </div> |]
        =]

        dynamic component target_panel(target t) [=
            component_class = "target_panel"
            id = t.name + "_panel"

            target_selector(t);
            target_state(t);
            target_exec(t);
            if (t.depends_on) [=
                for target d in t.depends_on [=
                    dependency_panel(t, d);
                =]
            =]
        =]
        
        dynamic component dependency_panel(target indep, target dep) [=
            component_class = "dependency_panel"
            id = dep.name + "_on_" + indep.name + "_panel"

            dependency_label(dep);
            dependency_state(indep, dep);        
            dependency_exec(dep);
        =]
        
        public dynamic component target_state(target t),(params{}) [=
            component_class = "target_state"
            name = "target_state"
            target_name [= 
                with (t) [= t.name; =]
                else     [= params{target_param}; =]
            =]
            id = target_name + "_state" 

            if (!completion_status{target_name}) [=
                set_completion_status(target_name, READY);
            =]

            status_box(completion_status{target_name});
        =]
    
        dynamic component target_selector(target t) [=
            component_class = "target_selector"
            id = t.name + "_selector"
            target_name = t.name
            exec_id = t.name + "_exec" 
        
            request_button(target_name, target_name, exec_id, target_exec, target_param, target_name);
        =]

        public dynamic component target_exec(target t),(params{}) [=
            component_class = "target_exec"
            name = "target_exec"
            target_name = params ? params{target_param} : t.name
            id = target_name + "_exec"
            target this_target = target_by_name{target_name}
            
            execute_target(target t) [=
                log("   ------ execute_target " + t.name);
                t.execute;
            =]

            if (!target_name) [=
                redirect unspecified_target_error(here)
            =]
            
            if (params{target_param} && completion_status{target_name} != EXECUTING) [=
                set_completion_status(target_name, EXECUTING);
                set_success_status(target_name, EXECUTING);

                /--- execute in a separate thread ---/
                execute_target(this_target);    // ++
                
            =] else if (!success_status{target_name}) [=
                set_success_status(target_name, READY);
            =]
            status_box(success_status{target_name});
        =]

        dynamic component dependency_label(target dep) [=
            dep.name;
        =]

        dynamic component dependency_state(target indep, target dep) [=
        =]

        dynamic component dependency_exec(target dep) [=
        =]

        /------- top-level wrapper around everything -------/
        [| <div id="main_container"> |]
        
        [| <div id="left_panel" class="table_panel"> |]     
        
        [| <h2> |]
        title;
        [| </h2> |]

        for target_group tg in all_target_groups [=
            target_group_panel(tg);
        =]
        [| </div> |]
        
        size_bar;
        
        [| <div id="right_panel"> |]
        bake_console;
        [| </div> |]

        [| </div> |]

        [| <script> |]
        size_bar_script;
        expand_panel_script;
        util_script;
        [| </script> |]      
    =]


    size_bar_script [|
        var main_container;
        var size_bar;
        var left_panel;
        var right_panel;
        var last_x;
        function size_bar_init() {
            main_container = document.getElementById("main_container");
            size_bar = document.getElementById("size_bar");
            left_panel = document.getElementById("left_panel");
            right_panel = document.getElementById("right_panel");
            size_bar.addEventListener("mousedown", size_bar_select, false);
            window.addEventListener("mouseup", size_bar_release, false);
        }
        function size_bar_release() {
            window.removeEventListener("mousemove", size_bar_drag, true);
            size_bar.style.cursor = "pointer";
            return false;
        }
        function size_bar_select(e) {
            window.addEventListener("mousemove", size_bar_drag, true);
            size_bar.style.cursor = "move";
            last_x = e.clientX;
            e.preventDefault();
            e.stopPropagation();
            return false;
        }
        function size_bar_drag(e) {
            var dx = e.clientX - last_x;
            last_x = e.clientX;
            var newLeft = (size_bar.offsetLeft + dx) + "px";
            var newRight = (size_bar.offsetLeft + size_bar.offsetWidth + dx) + "px";
            
            size_bar.style.left = newLeft;
            left_panel.style.width = newLeft;
            right_panel.style.left = newRight;
            e.preventDefault();
            e.stopPropagation();
            return false;
        }
    |]


    util_script [|
        function addClass(element, classToAdd) {
            var currentClassValue = element.className;
            if (currentClassValue.indexOf(classToAdd) == -1) {
                if ((currentClassValue == null) || (currentClassValue === "")) {
                    element.className = classToAdd;
                } else {
                    element.className += " " + classToAdd;
                }
            }
        }

        function removeClass(element, classToRemove) {
            var currentClassValue = element.className;

            if (currentClassValue == classToRemove) {
                element.className = "";
                return;
            }

            var classValues = currentClassValue.split(" ");
            var filteredList = [];

            for (var i = 0 ; i < classValues.length; i++) {
                if (classToRemove != classValues[i]) {
                    filteredList.push(classValues[i]);
                }
            }
            element.className = filteredList.join(" ");
        }
    |]    


    
    /------------------------------------------/
    /--------------  error pages  -------------/
    /------------------------------------------/

    page error_page(msg, ctx) [=
        [| <h2>Error: /]            
        msg;
        [| </h2> |]
        
        with (ctx) [=
            ctx;
        =]
    =]   

    error_page("Unspecified target.", ctx) unspecified_target_error(ctx) [/]

    error_page("Unknown target " + target_name, ctx) unknown_target_error(target_name, ctx) [/]

=]


